''
' IMG2ANS - IMG2ANS_BM (Module)
' 
' @depends IMG2ANS.BI
' @depends IMG2ANS.BAS
' @depends include/MOUSE/MOUSE.BM
' @depends include/InForm-PE/InForm/InFormCommon.bi
' @depends include/InForm-PE/InForm/InForm.bi
' @depends include/InForm-PE/InForm/InForm.ui
' @depends include/QB64_GJ_LIB/STRINGS/STRINGS.BM
'
$IF IMG2ANS_BM = UNDEFINED THEN
    $LET IMG2ANS_BM = TRUE
$END IF


''
' Sets input focus to image
' @param LONG pb picturebox control
'
SUB SetImgFocus(pb&)
    console.warn "SetImgFocus"
    ClearImgFocus
    Control(pb&).BorderColor = _RGB32(255, 0, 0)
    Control(pb&).Redraw = TRUE
END SUB


''
' Clears ImgFocus
'
SUB ClearImgFocus
    console.warn "ClearImgFocus"
    Control(SI_SourceImgPB).BorderColor  = _RGB32(167, 166, 170)
    Control(DP_DestPalPB).BorderColor    = _RGB32(167, 166, 170)
    Control(RC_ResizeCropPB).BorderColor = _RGB32(167, 166, 170)
    Control(P_PreviewPB).BorderColor     = _RGB32(167, 166, 170)
    Control(SI_SourceImgPB).Redraw       = TRUE
    Control(DP_DestPalPB).Redraw         = TRUE
    Control(RC_ResizeCropPB).Redraw      = TRUE
    Control(P_PreviewPB).Redraw          = TRUE
END SUB


''
' Load Image into PictureBox
' @param IMAGE img to load
' @param LONG pb Control ID of PictueBox
' @param STRING directory to start in for file picker
'
SUB LoadImageIntoPB(img AS IMAGE, pb&, directory$)
    console.warn "LoadImageIntoPB"
    img.file_path$  = BrowseForImageFile$(directory$)
    img.file_name$  = MID$(img.file_path$, _INSTRREV(img.file_path$, SLASH$) + 1)
    img.file_ext$   = MID$(img.file_name$, _INSTRREV(img.file_name$, "."))
    img.ID&         = pb&
    LoadImage Control(img.ID&), img.file_path$
    img.handle&     = Control(img.ID&).HelperCanvas&
    img.src_w%      = _WIDTH(img.handle&)
    img.src_h%      = _HEIGHT(img.handle&)
    img.src_x%      = 1
    img.src_y%      = 1
    img.dst_w%      = _WIDTH(img.handle&)
    img.dst_h%      = _HEIGHT(img.handle&)
    img.dst_x%      = 1
    img.dst_y%      = 1
    img.x_offset%   = 0
    img.y_offset%   = 0
    img.zoom!       = 1
    img.has_mouse%% = FALSE
    img.has_focus%% = TRUE
    CALL console.dir_image(img)
END SUB


''
' Browses for image file using OS file browser dialog
' @param STRING directory to start in for file picker
'
FUNCTION BrowseForImageFile$(directory$)
    console.info "BrowseForImageFile$"
    ' Choose an image file with dialog
    BrowseForImageFile$ = _OPENFILEDIALOG$( _
        "Choose an image", _
        directory$, _
        "*.jpg|*.jpeg|*.png|*.tga|*.bmp|*.psd|*.gif|*.pcx|*.svg|*.qoi", _
        "Image Files", _
        -1 _
    )
END FUNCTION


''
' Log IMAGE properties if DEBUGGING
' @param IMAGE image to log properties of
'
SUB console.dir_image(img AS IMAGE)
    console.warn _TRIM$(Control(img.ID&).Name$) + " {"
    console.log "   ID&         = " + _TRIM$(STR$(img.ID&))
    console.log "   handle&     = " + _TRIM$(STR$(img.handle&))
    console.log "   file_name$  = " + img.file_name$
    console.log "   file_path$  = " + img.file_path$
    console.log "   file_ext$   = " + img.file_ext$
    console.log "   src_w%      = " + _TRIM$(STR$(img.src_w%))
    console.log "   src_h%      = " + _TRIM$(STR$(img.src_h%))
    console.log "   src_x%      = " + _TRIM$(STR$(img.src_x%))
    console.log "   src_y%      = " + _TRIM$(STR$(img.src_y%))
    console.log "   dst_w%      = " + _TRIM$(STR$(img.dst_w%))
    console.log "   dst_h%      = " + _TRIM$(STR$(img.dst_h%))
    console.log "   dst_x%      = " + _TRIM$(STR$(img.dst_x%))
    console.log "   dst_y%      = " + _TRIM$(STR$(img.dst_y%))
    console.log "   x_offset%   = " + _TRIM$(STR$(img.x_offset%))
    console.log "   y_offset%   = " + _TRIM$(STR$(img.y_offset%))
    console.log "   has_mouse%% = " + _TRIM$(STR$(img.has_mouse%%))
    console.log "   has_focus%% = " + _TRIM$(STR$(img.has_focus%%))
    console.log "   zoom!       = " + _TRIM$(STR$(img.zoom!))
    console.warn "}"
END SUB


''
' Log a boxed message to console if DEBUGGING
' @param STRING msg message to send
' @param INTEGER kolor% color to use
'
SUB console.box(msg$, kolor%)
    $IF DEBUGGING = TRUE THEN
        DIM AS STRING e, color_code, lines, intensity
        DIM AS INTEGER fg, afg
        intensity$ = "0"
        e$ = CHR$(27)
        afg% = ANSI_COLOR%(kolor%)
        color_code$ = e$ + "["
        IF kolor% > 7 THEN intensity$ = "1"
        color_code$ = color_code$ + intensity$ + ";3"
        color_code$ = color_code$ + _TRIM$(STR$(afg%)) + "m"
        lines$ = "+" + STRING$(LEN(msg$)+2, "-") + "+"
        _ECHO color_code$ + lines$
        _ECHO "| " + msg$ + " |"
        _ECHO lines$ + e$ + "[0m"
    $END IF
    msg$ = ""
END SUB


''
' Log a banner to console if DEBUGGING
' @param STRING msg message to send
' @param INTEGER kolor% color to use
'
SUB console.banner(msg$, kolor%)
    $IF DEBUGGING = TRUE THEN
        DIM AS STRING e, color_code, intensity
        DIM AS INTEGER fg, afg
        intensity$ = "0"
        e$ = CHR$(27)
        afg% = ANSI_COLOR(kolor%)
        msg$ = STR.replace$(msg$, "\n", CHR$(10), -1)
        msg$ = STR.replace$(msg$, "\t", CHR$(9), -1)
        color_code$ = e$ + "["
        IF kolor% > 7 THEN intensity$ = "1"
        color_code$ = color_code$ + intensity$ + ";3"
        color_code$ = color_code$ + _TRIM$(STR$(afg%)) + "m"
        _ECHO color_code$ + msg$ + e$ + "[0m"
    $END IF
    msg$ = ""
END SUB


''
' Log to console if DEBUGGING
' @param STRING msg message to send
'
SUB console.log(msg$)
    $IF DEBUGGING = TRUE THEN
        _ECHO msg$
    $END IF
    msg$ = ""
END SUB


''
' Log to console as info if DEBUGGING
' @param STRING msg message to send
'
SUB console.info(msg$)
    $IF DEBUGGING = TRUE THEN
        DIM AS STRING e
        e$ = CHR$(27)
        _ECHO e$ + "[1;36m" + msg$ + e$ + "[0m"
    $END IF
    msg$ = ""
END SUB


''
' Log to console as warning if DEBUGGING
' @param STRING msg message to send
'
SUB console.warn(msg$)
    $IF DEBUGGING = TRUE THEN
        DIM AS STRING e
        e$ = CHR$(27)
        _ECHO e$ + "[1;33m" + msg$ + e$ + "[0m"
    $END IF
    msg$ = ""
END SUB


''
' Log to console as error if DEBUGGING
' @param STRING msg message to send
'
SUB console.error(msg$)
    $IF DEBUGGING = TRUE THEN
        DIM AS STRING e
        e$ = CHR$(27)
        _ECHO e$ + "[1;31m" + msg$ + e$ + "[0m"
    $END IF
    msg$ = ""
END SUB

'$INCLUDE:'include/MOUSE/MOUSE.BM'
'$INCLUDE:'include/QB64_GJ_LIB/STRINGS/STRINGS.BM'