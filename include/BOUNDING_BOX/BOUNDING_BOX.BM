''
' QB64_GJ_LIB
' GRYMMJACK'S BOUNDING BOX OBJECT
'
' A bounding box simply is a box with some styles, and it can be resized.
'
' @author Rick Christy <grymmjack@gmail.com>
'
' @depends QB64_GJ_LIB/_GJ_LIB_COMMON.BI
' @depends QB64_GJ_LIB/_GJ_LIB_COMMON.BM
' @depends BOUNDING_BOX.BI
'
$INCLUDEONCE


''
' Creates the bounding box
' @param __BOUNDING_BOX bb bounding box to target
' @param LONG dest_image destination image to draw to
' @param INTEGER x position
' @param INTEGER y position
' @param INTEGER w width
' @param INTEGER h height
'
SUB BBOX_create(bb AS __BOUNDING_BOX, dest_image&, x%, y%, w%, h%)
    'setup bounding box
    bb.pos.x        = x%
    bb.pos.y        = y%
    bb.w            = w%
    bb.h            = h%
    CALL BBOX_update_canvas(bb, dest_image&)
    CALL BBOX_render(bb)
END SUB


''
' Updates the canvases of the bounding box - used after create
' @param __BOUNDING_BOX bb bounding box to target
' @param LONG dest_image destination image bbox is drawn to
'
SUB BBOX_update_canvas(bb AS __BOUNDING_BOX, dest_image&)
    DIM AS LONG old_dest
    'create clean canvas
    old_dest = _DEST
    bb.clean_canvas = _NEWIMAGE(_WIDTH(dest_image&), _HEIGHT(dest_image&), 32)  
    _DEST bb.clean_canvas
    CLS
    _PUTIMAGE (0, 0), dest_image&, bb.clean_canvas
    _DEST old_dest
    bb.canvas = dest_image&
END SUB


''
' Renders the bounding box (called when visible changes occur)
' @param __BOUNDING_BOX bb bounding box to target
'
SUB BBOX_render(bb AS __BOUNDING_BOX)
    bb.status.ready = FALSE
    IF NOT bb.status.visible THEN
        _PUTIMAGE (0, 0), BBOX.clean_canvas, BBOX.canvas
    ELSE
        SELECT CASE bb.style$
            CASE "selected"
                BBOX_decorate bb, _
                    bb.styles.selected.border.fg, _
                    bb.styles.selected.border.w, _
                    bb.styles.selected.border.pattern
            CASE "deselected"
                BBOX_decorate bb, _
                    bb.styles.deselected.border.fg, _
                    bb.styles.deselected.border.w, _
                    bb.styles.deselected.border.pattern
            CASE "resizing"
                BBOX_decorate bb, _
                    bb.styles.resizing.border.fg, _
                    bb.styles.resizing.border.w, _
                    bb.styles.resizing.border.pattern 
            CASE "moving"
                BBOX_decorate bb, _
                    bb.styles.moving.border.fg, _
                    bb.styles.moving.border.w, _
                    bb.styles.moving.border.pattern 
            CASE "over"
                BBOX_decorate bb, _
                    bb.styles.over.border.fg, _
                    bb.styles.over.border.w, _
                    bb.styles.over.border.pattern
            CASE "idle"
                BBOX_decorate bb, _
                    bb.styles.idle.border.fg, _
                    bb.styles.idle.border.w, _
                    bb.styles.idle.border.pattern 
        END SELECT
    END IF
    bb.status.ready = TRUE
END SUB


''
' Decorate the bounding box
' @param __BOUNDING_BOX bb bounding box to decorate
' @param _UNSIGNED LONG fg foreground color of the box border
' @param INTEGER w width of the bounding box border
' @param LONG pattern to use for border line
'
SUB BBOX_decorate(bb AS __BOUNDING_BOX, fg~&, w%, pattern&)
    DIM AS INTEGER i
    DIM AS LONG old_dest : old_dest = _DEST
    _DEST bb.canvas
    'clear
    FOR i% = 0 TO w%
        LINE (bb.pos.x+i%, bb.pos.y+i%)-(bb.pos.x+bb.w-i%-1, bb.pos.y+bb.h-i%-1), _RGB32(0, 0, 0), B
    NEXT i%
    'decorate
    FOR i% = 0 TO w%
        LINE (bb.pos.x+i%, bb.pos.y+i%)-(bb.pos.x+bb.w-i%-1, bb.pos.y+bb.h-i%-1), fg~&, B, pattern&
    NEXT i%
    _DEST old_dest
END SUB


''
' Attempt to set the style of bounding box to new style
' @param __BOUNDING_BOX bb bounding box to target
' @param STRING new_style ("idle", "over", "selected", "resizing", "moving")
' @return INTEGER TRUE if new_state changed, FALSE If not
' 
FUNCTION BBOX_set_style%(bb AS __BOUNDING_BOX, new_style$)
    IF BBOX_state_can_become(bb, new_style$) THEN
        BBOX_set_style_state bb, new_style$
        BBOX_set_style% = TRUE
    ELSE
        BBOX_set_style% = FALSE
    END IF
END FUNCTION


''
' Set the bounding boxes style state
' @param __BOUNDING_BOX bb bounding box to target
' @param STRING style_state ("idle", "over", "selected", "resizing", "moving")
'
SUB BBOX_set_style_state(bb AS __BOUNDING_BOX, style_state$)
    IF INSTR("idle, over, selected, deselected, resizing, moving", style_state$) <> 0 THEN
        bb.style$ = style_state$
        CALL BBOX_render(bb)
    END IF
END SUB


''
' Checks if the state can transition to another state
' @param __BOUNDING_BOX bb bounding box to check
' @param STRING new_state the state to transition to
' @return INTEGER TRUE if can transition, FALSE otherwise
'
FUNCTION BBOX_state_can_become%(bb AS __BOUNDING_BOX, new_state$)
    SELECT CASE bb.style$
        CASE "selected"
            BBOX_state_can_become% = INSTR("deselected, resizing, moving", new_state$)
        CASE "deselected"
            BBOX_state_can_become% = INSTR("idle, selected, resizing, moving", new_state$)
        CASE "resizing"
            BBOX_state_can_become% = INSTR("selected, moving", new_state$)
        CASE "moving"
            BBOX_state_can_become% = INSTR("selected, resizing", new_state$)
        CASE "over"
            BBOX_state_can_become% = INSTR("deselected, idle, selected", new_state$)
        CASE "idle"
            BBOX_state_can_become% = INSTR("deselected, selected, over", new_state$)
    END SELECT
END FUNCTION


''
' Set the bounding box resizing status
' @param __BOUNDING_BOX bb bounding box to target
' @param INTEGER state% (bool) TRUE is resizing, FALSE not resizing
' @param STRING edge used to resize ("top", "bot", "left", "right")
'
SUB BBOX_set_resizing(bb AS __BOUNDING_BOX, state%, edge$)
    bb.status.resizing = state%
    SELECT CASE edge$
        CASE "top": 
            bb.status.resizing_top = state%
        CASE "bot":
            bb.status.resizing_bot = state%
        CASE "left":
            bb.status.resizing_left = state%
        CASE "right":
            bb.status.resizing_right = state%
    END SELECT
    CALL BBOX_render(bb)
END SUB


''
' Set the bounding box moving status
' @param __BOUNDING_BOX bb bounding box to target
' @param INTEGER state% (bool) TRUE is moving, FALSE not moving
' @param STRING edge used to resize ("up", "down", "left", "right")
'
SUB BBOX_set_moving(bb AS __BOUNDING_BOX, state%, edge$)
    bb.status.moving = state%
    SELECT CASE edge$
        CASE "up": 
            bb.status.moving_up = state%
        CASE "down":
            bb.status.moving_down = state%
        CASE "left":
            bb.status.moving_left = state%
        CASE "right":
            bb.status.moving_right = state%
    END SELECT
    CALL BBOX_render(bb)
END SUB


''
' Select the bounding box
' @param __BOUNDING_BOX bb bounding box to target
'
SUB BBOX_select(bb AS __BOUNDING_BOX)    
    IF BBOX_set_style(bb, "selected") THEN bb.status.selected = TRUE
    CALL BBOX_render(bb)
END SUB


''
' Deselect the bounding box
' @param __BOUNDING_BOX bb bounding box to target
'
SUB BBOX_deselect(bb AS __BOUNDING_BOX)
    IF BBOX_set_style(bb, "deselected") THEN bb.status.selected = FALSE
    CALL BBOX_render(bb)
END SUB


''
' Toggle the selected state of the bounding box
' @param __BOUNDING_BOX bb bounding box to toggle state for
'
SUB BBOX_toggle_selected(bb AS __BOUNDING_BOX)
    IF bb.status.selected THEN
        BBOX_deselect bb
    ELSE
        BBOX_select bb
    END IF
END SUB


''
' Set the bounding box visibility
' @param __BOUNDING_BOX bb bounding box to target
' @param _BYTE is_visible (bool) TRUE = shown, FALSE = hidden
'
SUB BBOX_set_visibility(bb AS __BOUNDING_BOX, is_visible%%)
    bb.status.visible = is_visible%%
    CALL BBOX_render(bb)
END SUB


''
' Set the bounding box position
' @param __BOUNDING_BOX bb bounding box to target
' @param INTEGER x position
' @param INTEGER y position
'
SUB BBOX_set_pos(bb AS __BOUNDING_BOX, x%, y%)
    _PUTIMAGE (0, 0), bb.clean_canvas, bb.canvas
    bb.pos.x% = x%
    bb.pos.y% = y%
    CALL BBOX_render(bb)
END SUB


''
' Set the bounding box width and height
' @param __BOUNDING_BOX bb bounding box to target
' @param INTEGER w width
' @param INTEGER h height
'
SUB BBOX_set_size(bb AS __BOUNDING_BOX, w%, h%)
    bb.w% = w%
    bb.h% = h%
    CALL BBOX_render(bb)
END SUB


''
' Hide the bounding box
' @param __BOUNDING_BOX bb bounding box to target
'
SUB BBOX_hide(bb AS __BOUNDING_BOX)
    _PUTIMAGE (0, 0), bb.clean_canvas, bb.canvas
    CALL BBOX_set_visibility(bb, FALSE)
END SUB


''
' Show the bounding box
' @param __BOUNDING_BOX bb bounding box to target
'
SUB BBOX_show(bb AS __BOUNDING_BOX)
    CALL BBOX_set_visibility(bb, TRUE)
END SUB


''
' Move the bounding box from the left edge
' @param __BOUNDING_BOX bb bounding box to target
' @param INTEGER amount to Move by (positive number for grow, neg for shrink)
'
SUB BBOX_move_left(bb AS __BOUNDING_BOX, amount%)
    IF BBOX_set_style(bb, "moving") THEN
        CALL BBOX_set_moving(bb, TRUE, "left")
        ' do Move
        CALL BBOX_render(bb)
    END IF
END SUB


''
' Move the bounding box from the right edge
' @param __BOUNDING_BOX bb bounding box to target
' @param INTEGER amount to Move by (positive number for grow, neg for shrink)
'
SUB BBOX_move_right(bb AS __BOUNDING_BOX, amount%)
    IF BBOX_set_style(bb, "moving") THEN
        CALL BBOX_set_moving(bb, TRUE, "right")
        ' do Move
        CALL BBOX_render(bb)
    END IF
END SUB


''
' Move the bounding box from the top edge
' @param __BOUNDING_BOX bb bounding box to target
' @param INTEGER amount to Move by (positive number for grow, neg for shrink)
'
SUB BBOX_move_up(bb AS __BOUNDING_BOX, amount%)
    IF BBOX_set_style(bb, "moving") THEN
        CALL BBOX_set_moving(bb, TRUE, "up")
        ' do Move
        CALL BBOX_render(bb)
    END IF
END SUB


''
' Move the bounding box from the bottom edge
' @param __BOUNDING_BOX bb bounding box to target
' @param INTEGER amount to Move by (positive number for grow, neg for shrink)
'
SUB BBOX_move_down(bb AS __BOUNDING_BOX, amount%)
    IF BBOX_set_style(bb, "moving") THEN
        CALL BBOX_set_moving(bb, TRUE, "down")
        ' do Move
        CALL BBOX_render(bb)
    END IF
END SUB


''
' Resize the bounding box from the left edge
' @param __BOUNDING_BOX bb bounding box to target
' @param INTEGER amount to resize by (positive number for grow, neg for shrink)
'
SUB BBOX_resize_left(bb AS __BOUNDING_BOX, amount%)
    IF BBOX_set_style(bb, "resizing") THEN
        CALL BBOX_set_resizing(bb, TRUE, "left")
        ' do resize
        CALL BBOX_render(bb)
    END IF
END SUB


''
' Resize the bounding box from the right edge
' @param __BOUNDING_BOX bb bounding box to target
' @param INTEGER amount to resize by (positive number for grow, neg for shrink)
'
SUB BBOX_resize_right(bb AS __BOUNDING_BOX, amount%)
    IF BBOX_set_style(bb, "resizing") THEN
        CALL BBOX_set_resizing(bb, TRUE, "right")
        ' do resize
        CALL BBOX_render(bb)
    END IF
END SUB


''
' Resize the bounding box from the top edge
' @param __BOUNDING_BOX bb bounding box to target
' @param INTEGER amount to resize by (positive number for grow, neg for shrink)
'
SUB BBOX_resize_top(bb AS __BOUNDING_BOX, amount%)
    IF BBOX_set_style(bb, "resizing") THEN
        CALL BBOX_set_resizing(bb, TRUE, "top")
        ' do resize
        CALL BBOX_render(bb)
    END IF
END SUB


''
' Resize the bounding box from the bottom edge
' @param __BOUNDING_BOX bb bounding box to target
' @param INTEGER amount to resize by (positive number for grow, neg for shrink)
'
SUB BBOX_resize_bot(bb AS __BOUNDING_BOX, amount%)
    IF BBOX_set_style(bb, "resizing") THEN
        CALL BBOX_set_resizing(bb, TRUE, "bot")
        ' do resize
        CALL BBOX_render(bb)
    END IF
END SUB


''
' Checks to see if the mouse pointer is inside or on the bounding box
' @param __BOUNDING_BOX bb to check
' @param INTEGER x point x position
' @param INTEGER y point y position
' @return INTEGER TRUE if inside or on, FALSE otherwise
'
FUNCTION BBOX_point_over%(bb AS __BOUNDING_BOX, x%, y%)
    BBOX_point_over% = (x% >= bb.pos.x AND x% <= bb.pos.x + bb.w) _
                   AND (y% >= bb.pos.y AND y% <= bb.pos.y + bb.h)
END FUNCTION


''
' Dumps a bounding box object
'
' @param __BOUNDING_BOX bb bounding box to dump
' @param STRING label$ to give the dump block
' @return STRING dump block
'
FUNCTION BBOX_dump$(bb AS __BOUNDING_BOX, label$)
    DIM AS STRING r, t1, t2
    t1$ = STRING$(4, " ") : t2$ = t1$ + t1$
    r$ = GJ_LIB_NL$ + "BBOX " + label$ + ": {" + GJ_LIB_NL$
    r$ = r$ + t1$ + ".w: " + BBOX_n$(bb.w, TRUE)
    r$ = r$ + t1$ + ".h: " + BBOX_n$(bb.h, TRUE)
    r$ = r$ + t1$ + ".pos.x: " + BBOX_n$(bb.pos.x, TRUE)
    r$ = r$ + t1$ + ".pos.y: " + BBOX_n$(bb.pos.y, TRUE)
    r$ = r$ + t1$ + ".style: " + bb.style$ + GJ_LIB_NL$
    r$ = r$ + t1$ + "status: {" + GJ_LIB_NL$
    r$ = r$ + t2$ + ".ready: " + BBOX_n$(bb.status.ready, TRUE)
    r$ = r$ + t2$ + ".visible: " + BBOX_n$(bb.status.visible, TRUE)
    r$ = r$ + t2$ + ".selected: " + BBOX_n$(bb.status.selected, TRUE)
    r$ = r$ + t2$ + ".resizing: " + BBOX_n$(bb.status.resizing, TRUE)
    r$ = r$ + t2$ + ".resizing_left: " + BBOX_n$(bb.status.resizing_left, TRUE)
    r$ = r$ + t2$ + ".resizing_right: " + BBOX_n$(bb.status.resizing_right, TRUE)
    r$ = r$ + t2$ + ".resizing_top: " + BBOX_n$(bb.status.resizing_top, TRUE)
    r$ = r$ + t2$ + ".resizing_bot: " + BBOX_n$(bb.status.resizing_bot, TRUE)
    r$ = r$ + t2$ + ".moving: " + BBOX_n$(bb.status.moving, TRUE)
    r$ = r$ + t2$ + ".moving_left: " + BBOX_n$(bb.status.moving_left, TRUE)
    r$ = r$ + t2$ + ".moving_right: " + BBOX_n$(bb.status.moving_right, TRUE)
    r$ = r$ + t2$ + ".moving_top: " + BBOX_n$(bb.status.moving_up, TRUE)
    r$ = r$ + t2$ + ".moving_bot: " + BBOX_n$(bb.status.moving_down, TRUE)
    r$ = r$ + t1$ + "}" + GJ_LIB_NL$
    r$ = r$ + "}"
    BBOX_dump$ = r$
END FUNCTION


''
' String helper for trimming numbers
' @param INTEGER ANY non float
' @param INTEGER new_line append?
' @return STRING trimmed number with or without newline
'
FUNCTION BBOX_n$(value, new_line)
    DIM r AS STRING
    r$ = _TRIM$(STR$(value))
    IF new_line THEN r$ = r$ + GJ_LIB_NL$
    BBOX_n$ = r$
END FUNCTION


''
' Dumps a bounding box style object
'
' @param __BOUNDING_BOX bb bounding box to dump
' @param STRING label$ to give the dump block
' @param STRING style$ bounding box style to dump
' @return STRING dump block
'
FUNCTION BBOX_dump_style$(bb AS __BOUNDING_BOX, label$, style$)
    DIM AS STRING r, t1, t2
    DIM s AS __STYLE
    SELECT CASE style$
        CASE "idle"
            s = bb.styles.idle
        CASE "over"
            s = bb.styles.over
        CASE "selected"
            s = bb.styles.selected
        CASE "resizing"
            s = bb.styles.resizing
        CASE "moving"
            s = bb.styles.moving
    END SELECT
    t1$ = STRING$(4, " "): t2$ = t1$ + t1$
    r$ = GJ_LIB_NL$ + "BBOX.style." + style$ + " " + label$ + ": {" + GJ_LIB_NL$
    r$ = r$ + t1$ + ".fg: " + BBOX_n$(s.fg, TRUE)
    r$ = r$ + t1$ + ".bg: " + BBOX_n$(s.bg, TRUE)
    r$ = r$ + t1$ + ".border: { "+ GJ_LIB_NL$
    r$ = r$ + t2$ + ".fg: " + BBOX_n$(s.border.fg, TRUE)
    r$ = r$ + t2$ + ".w: " + BBOX_n$(s.border.w, TRUE)
    r$ = r$ + t2$ + ".pattern: " + BBOX_n$(s.border.pattern, TRUE)
    r$ = r$ + t1$ + "}"+ GJ_LIB_NL$
    r$ = r$ + "}"
    BBOX_dump_style$ = r$
END FUNCTION
